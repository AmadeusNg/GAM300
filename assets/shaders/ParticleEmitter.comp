#version 450 core

struct Particle {
    //transform params
    vec3 Size;//xyz scale of the particle
    vec3 Rotation;
    vec3 CurrentPosition;// constant transform position of the entity, position of particle is an offset from here based on velocity and acceleration
    
    //movement params
    vec3 Velocity;
    vec3 Acceleration;
    float Age;//how long has the particle been active
    
    //aesthetics params
    vec3 Color;//rgb color of the particle
    bool Active;//is the particle active
};

struct ParticleEmitter{
    float Life;
    vec3 Spawnoffset;
    vec3 Velocity;
    vec3 Acceleration;
    vec3 Position;
    vec3 Size;
    vec4 Color;
};

//writing the position of the particle out into a list to vertex buffer
layout (std140, binding = 1) buffer ParticleBuffer {
    Particle List[];
}v_ParticleOut;

//buffer access with particle shader?
layout (std140, binding = 2) coherent restrict buffer FreelistBuffer {
    int count;
    int indices[];
}v_Freelist;

layout (set = 0, binding = 3 ) buffer EmitterThingy{
    int particleamount;
    ParticleEmitter Emitter;
}EmitterStuff;

void MakeParticle(out Particle newparticle){
    //constant spawn point from emitter location
    newparticle.Age = EmitterStuff.Emitter.Life;
    newparticle.Velocity = EmitterStuff.Emitter.Velocity;
    newparticle.Acceleration =  EmitterStuff.Emitter.Acceleration;
    newparticle.Color = EmitterStuff.Emitter.Color.xyz;

    newparticle.CurrentPosition = EmitterStuff.Emitter.Position + EmitterStuff.Emitter.Spawnoffset;
    newparticle.Size = EmitterStuff.Emitter.Size;
    newparticle.Rotation = vec3(0.0,0.0,0.0);
    newparticle.Active = true;
}

layout(local_size_x = 64, local_size_y = 1, local_size_z = 1) in;

void main(){
    uint index = gl_GlobalInvocationID.x;

    if (index >= EmitterStuff.particleamount)
        return;
    
    int freelistindex = atomicAdd(v_Freelist.count, -1) -1;
    //if there are no free particles, return
    if (freelistindex < 0){
        atomicAdd(v_Freelist.count, 1);
        return;
    }

    //get the index of the free particle
    int particleindex = v_Freelist.indices[freelistindex];
    //create a new particle
    MakeParticle(v_ParticleOut.List[particleindex]);
}